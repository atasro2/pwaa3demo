gNumMusicPlayers = 4;
gMaxLines = 0;
SECTIONS {
    /* TODO: matching hack for later functions REMOVE!!!! */
    /* add any .rodata stuff that is used multiple times for easy C conversion here*/  

    . = 0x2000000;
    ewram (NOLOAD) :
    ALIGN(4)
    {
        INCLUDE "sym_ewram.txt"
    }
    . = 0x3000000;
    iwram (NOLOAD) :
    ALIGN(4)
    {
        INCLUDE "sym_iwram.txt"
    }
    . = 0x8000000;
    .text :
    ALIGN(4)
    {
        src/crt0.o(.text);
        src/main.o(.text);
        src/debug.o(.text);
        asm/notdebug.o(.text);
        src/bg.o(.text);
        asm/bg.o(.text);
        src/utils.o(.text);
        asm/utils.o(.text);
        src/utils_2.o(.text);
        src/case_data.o(.text);
        src/title_processes.o(.text);
        asm/gameover_process.o(.text);
        src/save_processes.o(.text);
        asm/save_processes.o(.text);
        asm/episode_load_processes.o(.text);
        asm/court.o(.text);
        asm/rom_BC08.o(.text);
        asm/investigation.o(.text);
        asm/court_record.o(.text);
        src/sound.o(.text);
        asm/animation.o(.text);
        src/movietest.o(.text);
        src/psyche_lock.o(.text);
        asm/hp_bar.o(.text);
        asm/signal_detector.o(.text);
        src/script_main.o(.text);
        asm/script_commands.o(.text);
        asm/opening.o(.text);
        asm/rom_1D308.o(.text);
        /* library functions */
        src/m4a_1.o(.text);
        src/m4a.o(.text);
        src/libagbsyscall.o(.text);
        src/agb_sram.o(.text);
        *libgcc.a:*_call_via_rX*.o(.text*);
        *libgcc.a:*_divsi3*.o(.text*);
        *libgcc.a:*_dvmd_tls*.o(.text*);
        *libgcc.a:*_modsi3*.o(.text*);
        *libgcc.a:*_udivsi3*.o(.text*);
        *libgcc.a:*_umodsi3*.o(.text*);
        *libc.a:*memcpy*.o(.text*);
    } =0
    .rodata :
    ALIGN(4)
    {
        src/main.o(.rodata);
        src/debug.o(.rodata);
        rodata/rodata.o(.rodata);
        src/sound.o(.rodata);
        rodata/rodata_2.o(.rodata);
        src/script_main.o(.rodata);
        rodata/script_main.o(.rodata);
        rodata/rodata_3.o(.rodata);
        src/m4a_tables.o(.rodata);
        rodata/song_data.o(.rodata);
        src/agb_sram.o(.rodata);
    } =0
    /* capcom :^)) */
    . += 0x100;
    .data :
    ALIGN(4)
    {
        src/main.o(.data);
        src/debug.o(.data);
        data/data.o(.data);
        src/sound.o(.data);
        data/data_2.o(.data);
        data/script_main.o(.data);
    } =0
    . = 0x8180000;
    gfx_data :
    ALIGN(4)
    {
        data/graphics.o(gfx_data);
    } =0

    /*
    script_data :
    ALIGN(4)
    {
    } =0
    */
    /* DWARF debug sections.
    Symbols in the DWARF debugging sections are relative to the beginning
    of the section so we begin them at 0.  */

    /* DWARF 1 */
    .debug          0 : { *(.debug) }
    .line           0 : { *(.line) }

    /* GNU DWARF 1 extensions */
    .debug_srcinfo  0 : { *(.debug_srcinfo) }
    .debug_sfnames  0 : { *(.debug_sfnames) }

    /* DWARF 1.1 and DWARF 2 */
    .debug_aranges  0 : { *(.debug_aranges) }
    .debug_pubnames 0 : { *(.debug_pubnames) }

    /* DWARF 2 */
    .debug_info     0 : { *(.debug_info .gnu.linkonce.wi.*) }
    .debug_abbrev   0 : { *(.debug_abbrev) }
    .debug_line     0 : { *(.debug_line) }
    .debug_frame    0 : { *(.debug_frame) }
    .debug_str      0 : { *(.debug_str) }
    .debug_loc      0 : { *(.debug_loc) }
    .debug_macinfo  0 : { *(.debug_macinfo) }

    /* Discard everything not specifically mentioned above. */

    /DISCARD/ :
    {
        *(*);
    }

}
